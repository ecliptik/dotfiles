function prompt_midna_precmd {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    branch_format="(${_prompt_midna_colors[2]}%b%f%u%c${_prompt_midna_colors[4]}●%f)"
  else
    branch_format="(${_prompt_midna_colors[2]}%b%f%u%c)"
  fi

  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"

  vcs_info 'prompt'

  if (( $+functions[python-info] )); then
    python-info
  fi
}

function prompt_midna_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_midna_precmd

  # Use extended color pallete if available.
  _prompt_midna_colors=(
    "%F{cyan}"
    "%F{yellow}"
    "%F{magenta}"
    "%F{red}"
    "%F{green}"
    "%F{white}"
    "%F{reset}"
  )

  # Formats:
  #   %b - branchname
  #   %u - unstagedstr (see below)
  #   %c - stagedstr (see below)
  #   %a - action (e.g. rebase-i)
  #   %R - repository path
  #   %S - path in the repository
  local branch_format="(${_prompt_midna_colors[1]}%b%f%u%c)"
  local action_format="(${_prompt_midna_colors[5]}%a%f)"
  local unstaged_format="${_prompt_midna_colors[4]}●%f"
  local staged_format="${_prompt_midna_colors[5]}●%f"

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*:prompt:*' check-for-changes true
  zstyle ':vcs_info:*:prompt:*' unstagedstr "${unstaged_format}"
  zstyle ':vcs_info:*:prompt:*' stagedstr "${staged_format}"
  zstyle ':vcs_info:*:prompt:*' actionformats "${branch_format}${action_format}"
  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"
  zstyle ':vcs_info:*:prompt:*' nvcsformats   ""

  zstyle ':prezto:module:editor' key-bindings 'vi'
  zstyle ':prezto:module:editor:info:keymap:alternate' format '%B%F{red}❮%f%b'

  # Set python-info parameters.
  zstyle ':prezto:module:python:info:virtualenv' format '(%v)'

  # Define prompts.
  PROMPT="
${_prompt_midna_colors[1]}%m%f in ${_prompt_midna_colors[3]}%~%f "'${vcs_info_msg_0_}'"
"'$python_info[virtualenv]'"${_prompt_midna_colors[3]}%% ${_prompt_midna_colors[7]}"
  RPROMPT=''
}

prompt_midna_setup "$@"
