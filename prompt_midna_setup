function prompt_midna_precmd {
  BRANCH=`printf "\ue0a0"`
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    branch_format="($BRANCH|${_prompt_midna_colors[2]}%b%f%u%c${_prompt_midna_colors[4]}‚óè%f)"
  else
    branch_format="($BRANCH|${_prompt_midna_colors[2]}%b%f%u%c)"
  fi

  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"

  vcs_info 'prompt'

  #Set title correctly
  case $TERM in
    xterm*|screen*)
      precmd () {print -Pn "\e]0;%m:%~\a"}
    ;;
  esac
}

function prompt_midna_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_midna_precmd

  # Use extended color pallete if available.
  _prompt_midna_colors=(
    "%F{cyan}"
    "%F{yellow}"
    "%F{magenta}"
    "%F{red}"
    "%F{green}"
    "%F{white}"
    "%F{reset}"
  )

  # Formats:
  #   %b - branchname
  #   %u - unstagedstr (see below)
  #   %c - stagedstr (see below)
  #   %a - action (e.g. rebase-i)
  #   %R - repository path
  #   %S - path in the repository
  local branch_format="(${_prompt_midna_colors[1]}%b%f%u%c)"
  local action_format="(${_prompt_midna_colors[5]}%a%f)"
  local unstaged_format="${_prompt_midna_colors[4]}‚óè%f"
  local staged_format="${_prompt_midna_colors[5]}‚óè%f"

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*:prompt:*' check-for-changes true
  zstyle ':vcs_info:*:prompt:*' unstagedstr "${unstaged_format}"
  zstyle ':vcs_info:*:prompt:*' stagedstr "${staged_format}"
  zstyle ':vcs_info:*:prompt:*' actionformats "${branch_format}${action_format}"
  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"
  zstyle ':vcs_info:*:prompt:*' nvcsformats   ""

  # Define prompts.
  source ${HOME}/dotfiles/kube-ps1/kube-ps1.sh
  PROMPT=" "'${vcs_info_msg_0_}'"
${_prompt_midna_colors[1]}%3/%f ""${_prompt_midna_colors[3]}üê≥  ${_prompt_midna_colors[7]}"
  PROMPT='$(kube_ps1)'$PROMPT
  RPROMPT=''
}

prompt_midna_setup "$@"
